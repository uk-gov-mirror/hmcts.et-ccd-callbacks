plugins {
    id 'application'
    id 'java'
    id 'checkstyle'
    id 'jacoco'
    id 'io.spring.dependency-management' version '1.1.5'
    id 'org.springframework.boot' version '3.3.0'
    id 'org.owasp.dependencycheck' version '9.2.0'
    id 'com.github.ben-manes.versions' version '0.51.0'
    id 'au.com.dius.pact' version '4.3.15'
    id 'io.freefair.lombok' version '8.14.2'
    id 'org.sonarqube' version '6.3.1.5724'
    id 'pmd'
    id 'com.github.hmcts.rse-cft-lib' version '0.19.1921'
}

group = 'uk.gov.hmcts.et'
version = '0.0.1'

pmd {
    toolVersion = "7.0.0"
    sourceSets = [sourceSets.main, sourceSets.test, sourceSets.cftlib]
    reportsDir = file("$project.buildDir/reports/pmd")
    // https://github.com/pmd/pmd/issues/876
    ruleSets = []
    ruleSetFiles = files("config/pmd/ruleset.xml")
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

checkstyle {
    maxWarnings = 0
    toolVersion = '10.14.0'
    getConfigDirectory().set(new File(rootDir, 'config/checkstyle'))
}

def sonarExclusions =
        ['**/uk/gov/hmcts/ethos/replacement/docmosis/config/**',
         '**/uk/gov/hmcts/ethos/replacement/docmosis/model/**',
         '**/uk/gov/hmcts/reform/ethos/replacement/docmosis/CaseOrchestrationApplication',
         '**/uk/gov/hmcts/ethos/replacement/docmosis.config.DevQueueClient.java',
         '**/uk/gov/hmcts/ethos/replacement/docmosis/domain/admin/**',
         '**/uk/gov/hmcts/ethos/replacement/docmosis/controllers/RespondentTellSomethingElseController.java'
]

sonarqube {
    properties {
        property "sonar.exclusions", sonarExclusions.join(', ')
        property "sonar.projectKey", "et-ccd-callbacks"
        property "sonar.dynamicAnalysis", "reuseReports"
        property "sonar.dependencyCheck.reportPath", "${project.buildDir}/reports/dependency-check-report.xml"
        property "sonar.coverage.jacoco.xmlReportPaths", "${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml"
    }
}

sourceSets {
    test {
        java {
            srcDir 'src/test/java'
            srcDir 'src/test/utils/java'
        }
    }
    functional {
        java {
            srcDir 'src/test/functional/java'
            srcDir 'src/test/utils/java'
        }
        resources {
            srcDir 'src/test/functional/resources'
        }
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
    apiTest {
        java {
            srcDir 'src/test/apiTest/java'
            srcDir 'src/test/utils/java'
        }
        resources {
            srcDir 'src/test/apiTest/resources'
        }
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
    contractTest {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            java.srcDir 'src/test/contractTest/java'
            resources.srcDir 'src/test/contractTest/resources'
        }
        resources.srcDir file('src/test/contractTest/resources')
    }
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/test/integration/java')
        }
        resources.srcDir file('src/test/integration/resources')
    }
}

configurations {
    functionalImplementation.extendsFrom testImplementation
    functionalRuntimeOnly.extendsFrom testRuntimeOnly
    contractTestImplementation.extendsFrom testImplementation
    contractTestRuntimeOnly.extendsFrom testRuntimeOnly
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
    apiTestImplementation.extendsFrom testImplementation
    apiTestRuntimeOnly.extendsFrom testRuntimeOnly
    configureEach {
        resolutionStrategy.eachDependency { details ->
            if (details.requested.group.startsWith("com.fasterxml.jackson")) {
                details.useVersion("$jacksonVersion")
            }
        }
    }
}

test {
    exclude '**/*ComponentTest*'
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

tasks.register('smoke', Test) {
    dependsOn ':codeceptSmoke'
    testClassesDirs = sourceSets.functional.output.classesDirs
    classpath = sourceSets.functional.runtimeClasspath

    useJUnitPlatform {
        includeTags 'smokeTest'
    }
    testLogging {
        events "passed", "skipped", "failed"
    }
    outputs.upToDateWhen { false }
    systemProperty('VAULTNAME', System.getenv('VAULTNAME'))
}

tasks.register('functional', Test) {
    testClassesDirs = sourceSets.apiTest.output.classesDirs
    classpath = sourceSets.apiTest.runtimeClasspath

    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }

    systemProperty('VAULTNAME', System.getenv('VAULTNAME'))
    maxHeapSize = '1G'
}

tasks.register('yarnInstall', Exec) {
    workingDir '.'
    commandLine 'yarn', 'install'
}

tasks.register('yarnAudit', Exec) {
    dependsOn ':yarnInstall'
    workingDir '.'
    commandLine 'yarn', 'npm', 'audit', '--groups', 'dependencies', '--json', '|', 'grep', 'auditAdvisory', '>', 'yarn-audit-known-issues'
}

tasks.register('codeceptSmoke', Exec) {
    dependsOn ':yarnInstall'
    commandLine 'yarn', 'test:smoke'
}

tasks.register('codeceptFunctional', Exec) {
    dependsOn 'yarnInstall'
    dependsOn 'functional'
    commandLine 'yarn', 'test:functional'
}

tasks.register('crossbrowser') {
    dependsOn ':codeceptFunctionalSauce'
    group = 'Delivery pipeline'
    description = 'Execute cross browser tests on saucelabs'
}

tasks.register('nightlyFunctional') {
    dependsOn ':codeceptFunctionalNightly'
    group = 'Delivery pipeline'
    description = 'Execute Functional Tests on nightly pipeline'
}

tasks.register('codeceptFunctionalSauce', Exec) {
    dependsOn ':yarnInstall'
    //IGNORE_SESSION_VALIDATION=true NODE_ENV=testing LOG_LEVEL=OFF NODE_PATH=. node_modules/.bin/codeceptjs run-multiple  ${BROWSER_GROUP:-'--all'} -c src/test/end-to-end/saucelabs.conf.js --grep @RET-XB --steps --reporter mocha-multi
    workingDir '.'
    commandLine './bin/run-crossbrowser-tests.sh'
    commandLine 'yarn', 'test:crossbrowser'
}

tasks.register('codeceptFunctionalNightly', Exec) {
    dependsOn 'yarnInstall'
    commandLine 'yarn', 'test:fullfunctional'
}

tasks.register('integration', Test) {
    description = "Runs integration tests"
    group = "Verification"
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    failFast = true
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
    reports {
        html.required = true
        html.outputLocation = file("${buildDir}/integration/")
    }
}

project.tasks['check'].dependsOn integration

project.ext {
    pacticipant = 'ethos_replDocmosisService'
    pacticipantVersion = getCheckedOutGitCommitHash()
}

static def getCheckedOutGitCommitHash() {
    'git rev-parse --verify --shirt HEAD'.execute().text.trim()
}

tasks.register('runContractTests', Test) {
    onlyIf {
        return System.env.SKIP_CONTRACT_TESTS == null || System.env.SKIP_CONTRACT_TESTS == 'false'
    }
    useJUnitPlatform()
    testClassesDirs = sourceSets.contractTest.output.classesDirs
    classpath = sourceSets.contractTest.runtimeClasspath
    systemProperty 'pact.rootDir', "pacts"
    testLogging.showStandardStreams = true
}

tasks.register('runAndPublishConsumerPactTests', Test) {
    logger.lifecycle("Runs pact consumer Tests")

    testClassesDirs = sourceSets.contractTest.output.classesDirs
    classpath = sourceSets.contractTest.runtimeClasspath
}

rootProject.tasks.named("processContractTestResources") {
    duplicatesStrategy = 'include'
}

rootProject.tasks.named("processFunctionalResources") {
    duplicatesStrategy = 'include'
}

runAndPublishConsumerPactTests.finalizedBy pactPublish

pact {
    broker {
        pactBrokerUrl = System.getenv("PACT_BROKER_FULL_URL") ?: 'http://localhost:80'
    }
    publish {
        pactDirectory = 'pacts'
        tags = [System.getenv("PACT_BRANCH_NAME") ?: 'Dev']
        version = project.pacticipantVersion
    }
}

tasks.register('fortifyScan', JavaExec) {
    main = "uk.gov.hmcts.fortifyclient.FortifyClientMainApp"
    classpath += sourceSets.test.runtimeClasspath
    jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
}

jacocoTestReport {
    executionData(test)
    reports {
        xml.required = true
        csv.required = false
        xml.outputLocation = file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml")
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.0
            }
        }
    }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
    // Specifies if the build should be failed if a CVSS score above a specified level is identified.
    // range of 0-10 fails the build, anything greater and it doesn't fail the build
    failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
    suppressionFile = 'config/owasp/suppressions.xml'
    analyzers.assemblyEnabled = false
    analyzers.nodeAudit.yarnEnabled = false
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://pkgs.dev.azure.com/hmcts/Artifacts/_packaging/hmcts-lib/maven/v1" }
}

project.tasks['sonarqube'].dependsOn test, integration, jacocoTestReport
check.dependsOn integration
checkstyleMain.shouldRunAfter(compileJava)
test.shouldRunAfter(checkstyleTest)
integration.shouldRunAfter(checkstyleIntegrationTest)
integration.shouldRunAfter(test)

ext {
    junitJupiterVersion = '5.13.4'
    junitPlatformVersion = '1.13.4'
    log4JVersion = '2.25.1'
    pact_version = '4.3.10'
    reformLoggingVersion = '6.0.1'
    serenity = '3.9.8'
    tomcatEmbedVersion = '10.1.46'
    jacksonVersion = '2.18.3'
}

ext["rest-assured.version"] = '5.1.0'

dependencyManagement {
    dependencies {
        dependency 'org.apache.httpcomponents.core5:httpcore5:5.3.5'
        dependencySet(group: 'com.google.guava', version: '33.4.8-jre') {
            entry 'guava'
        }
        dependencySet(group: 'io.netty', version: '4.2.6.Final') {
            entry 'netty-buffer'
            entry 'netty-codec'
            entry 'netty-codec-dns'
            entry 'netty-codec-http'
            entry 'netty-codec-http2'
            entry 'netty-codec-socks'
            entry 'netty-common'
            entry 'netty-handler'
            entry 'netty-handler-proxy'
            entry 'netty-resolver'
            entry 'netty-resolver-dns'
            entry 'netty-resolver-dns-classes-macos'
            entry 'netty-resolver-dns-native-macos'
            entry 'netty-transport'
            entry 'netty-transport-native-epoll'
            entry 'netty-transport-native-kqueue'
            entry 'netty-transport-native-unix-common'
            entry 'netty-transport-classes-epoll'
            entry 'netty-transport-classes-kqueue'
        }
    }
}

dependencies {

    implementation group: 'com.github.hmcts', name: 'et-common', version: '3.1.1'
    implementation group: 'com.github.hmcts', name: 'et-data-model', version: '4.3.1'
    implementation group: 'com.github.hmcts', name: 'ecm-common', version: '3.0.1'
    implementation group: 'com.github.hmcts', name: 'ecm-data-model', version: '2.0.3'
    implementation group: 'uk.gov.dwp.regex', name: 'postcode-validation', version: '1.1.0'
    implementation group: 'com.github.hmcts', name: 'ccd-case-document-am-client', version: '1.59.2'
    implementation group: 'com.github.hmcts', name: 'document-management-client', version: '7.0.1'
    implementation group: 'com.github.hmcts.java-logging', name: 'logging', version: '6.1.9'
    implementation group: 'net.logstash.logback', name: 'logstash-logback-encoder', version: '8.1'
    implementation group: 'com.nimbusds', name: 'nimbus-jose-jwt', version: '10.5'

    // Log4J core added to remove startup error message
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: log4JVersion
    // CVE-2025-54988
    implementation group: 'org.apache.tika', name: 'tika-core', version: '3.2.3'

    implementation group: 'commons-io', name: 'commons-io', version: '2.20.0'
    implementation group: 'commons-fileupload', name: 'commons-fileupload', version: '1.6.0'
    implementation group: 'io.netty', name: 'netty-tcnative-classes', version: '2.0.73.Final'
    implementation group: 'org.json', name: 'json', version: '20250107'
    implementation group: 'org.apache.commons', name: 'commons-compress', version: '1.28.0'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.18.0'
    implementation group: 'org.apache.httpcomponents.client5', name: 'httpclient5', version: '5.5'
    implementation group: 'org.apache.httpcomponents.core5', name: 'httpcore5', version: '5.3.5'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: log4JVersion
    implementation group: 'org.apache.logging.log4j', name: 'log4j-to-slf4j', version: log4JVersion
    implementation group: 'org.apache.pdfbox', name: 'pdfbox', version: '3.0.0-RC1'
    implementation group: 'org.apache.santuario', name: 'xmlsec', version: '4.0.4'
    implementation group: 'org.apache.tomcat.embed', name: 'tomcat-embed-core', version: tomcatEmbedVersion
    implementation group: 'org.apache.tomcat.embed', name: 'tomcat-embed-websocket', version: tomcatEmbedVersion
    implementation group: 'org.springframework.security', name: 'spring-security-crypto', version: '6.5.3'
    //To Remove vulnerability CVE-2020-11988
    implementation group: 'org.apache.xmlgraphics', name: 'xmlgraphics-commons', version: '2.11'
    implementation group: 'org.flywaydb', name: 'flyway-core', version: '9.22.3'
    implementation group: 'org.postgresql', name: 'postgresql', version: '42.7.7'
    implementation group: 'org.slf4j', name: 'slf4j-ext', version: '2.0.17'
    implementation group: 'org.springdoc', name: 'springdoc-openapi-starter-webmvc-ui', version: '2.5.0'
    implementation 'org.springframework.boot:spring-boot-configuration-processor'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'jakarta.servlet:jakarta.servlet-api:6.1.0'
    implementation 'javax.servlet:javax.servlet-api:4.0.1'
    implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign', version: '4.1.2'
    implementation group: 'org.springframework.retry', name: 'spring-retry', version: '2.0.12'
    // CVE-2022-25857
    implementation group: 'org.yaml', name: 'snakeyaml', version: '2.5'
    implementation group: 'uk.gov.service.notify', name: 'notifications-java-client', version: '5.2.1-RELEASE'
    implementation group: 'commons-validator', name: 'commons-validator', version: '1.10.0'
    // 1084765
    implementation group: 'org.webjars.npm', name: 'minimatch', version: '9.0.5'
    implementation group: 'org.webjars', name: 'webjars-locator-core', version: '0.59'
    implementation group: 'org.asynchttpclient', name: 'async-http-client-netty-utils', version: '2.12.4'
    implementation group: 'com.fasterxml.woodstox', name: 'woodstox-core', version: '7.1.1'
    implementation group: 'com.squareup.okio', name: 'okio', version: '3.16.0'
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.5.6'
    implementation group: 'ch.qos.logback', name: 'logback-core', version: '1.5.6'
    // CVE-2025-48734
    implementation group: 'commons-beanutils', name: 'commons-beanutils', version: '1.11.0'
    implementation 'jakarta.annotation:jakarta.annotation-api:2.1.1'
    implementation group: 'com.launchdarkly', name: 'launchdarkly-java-server-sdk', version: '7.10.2'

    runtimeOnly group: 'com.squareup.okio', name: 'okio-jvm', version: '3.16.0'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'com.github.hmcts:fortify-client:1.4.10:all', {
        exclude module: 'commons-io'
    }

    testImplementation group: 'com.auth0', name: 'java-jwt', version: '4.5.0'
    testImplementation group: 'com.warrenstrange', name: 'googleauth', version: '1.5.0'
    testImplementation group: 'net.serenity-bdd', name: 'serenity-core', version: serenity
    testImplementation group: 'net.serenity-bdd', name: 'serenity-junit', version: serenity
    testImplementation group: 'net.serenity-bdd', name: 'serenity-rest-assured', version: serenity, {
        exclude module: 'jaxb-osgi'
    }
    testImplementation group: 'net.serenity-bdd', name: 'serenity-spring', version: serenity
    testImplementation (group: 'org.docx4j', name: 'docx4j', version: '6.1.2') {
        exclude module: 'slf4j-log4j12'
    }
    testImplementation(platform('org.junit:junit-bom:5.13.4'))
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junitJupiterVersion
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: junitJupiterVersion
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junitJupiterVersion
    testRuntimeOnly group: 'org.junit.platform', name: 'junit-platform-launcher', version: junitPlatformVersion
    testRuntimeOnly group: 'org.junit.platform', name: 'junit-platform-engine', version: junitPlatformVersion
    testRuntimeOnly group: 'org.junit.platform', name: 'junit-platform-commons', version: junitPlatformVersion
    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'
    testImplementation group: 'org.testcontainers', name: 'postgresql', version: '1.21.3'
    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.27.4'
    // Mockito 5.0.0+ has merged mockito-inline into mockito-core
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '5.19.0'
    testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '5.19.0'
    // ByteBuddy dependencies required for Mockito with Java 21
    testImplementation group: 'net.bytebuddy', name: 'byte-buddy', version: '1.17.7'
    testImplementation group: 'net.bytebuddy', name: 'byte-buddy-agent', version: '1.17.7'
    testImplementation group: 'com.squareup.okhttp3', name: 'mockwebserver', version: '4.12.0'

    // Contract Tests
    contractTestImplementation group: 'au.com.dius.pact.consumer', name: 'junit5', version: '4.6.17'
    contractTestImplementation group: 'au.com.dius.pact.consumer', name: 'java8', version: '4.1.43'
    contractTestImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: '3.3.0'

    contractTestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junitJupiterVersion
    contractTestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: junitJupiterVersion
    contractTestRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junitJupiterVersion
    contractTestRuntimeOnly group: 'org.junit.platform', name: 'junit-platform-commons', version: junitPlatformVersion
    contractTestRuntimeOnly group: 'org.junit.platform', name: 'junit-platform-launcher', version: junitPlatformVersion
    contractTestRuntimeOnly group: 'org.junit.platform', name: 'junit-platform-engine', version: junitPlatformVersion
    contractTestImplementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign', version: '4.1.2'
    contractTestImplementation sourceSets.main.runtimeClasspath
    contractTestImplementation sourceSets.test.runtimeClasspath

    integrationTestImplementation group: 'commons-io', name: 'commons-io', version:  '2.20.0'
    integrationTestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junitJupiterVersion
    integrationTestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: junitJupiterVersion
    integrationTestRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junitJupiterVersion
    integrationTestRuntimeOnly group: 'org.junit.platform', name: 'junit-platform-launcher', version: junitPlatformVersion
    integrationTestRuntimeOnly group: 'org.junit.platform', name: 'junit-platform-engine', version: junitPlatformVersion
    integrationTestRuntimeOnly group: 'org.junit.platform', name: 'junit-platform-commons', version: junitPlatformVersion
    integrationTestAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.40'

    cftlibImplementation group: 'org.mockito', name: 'mockito-core', version: '5.19.0'
    cftlibImplementation 'org.springframework.boot:spring-boot-devtools'
}

bootJar {
    getArchiveFileName().set(provider {
        'et-cos.jar'
    })

    manifest {
        attributes('Implementation-Version': project.version.toString())
    }
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

bootWithCCD {
    // Simulates idam and s2s locally.
    // See rse-cft-lib/LibRunnerTask
    authMode = uk.gov.hmcts.rse.AuthMode.Local

    environment 'RSE_LIB_ADDITIONAL_DATABASES', 'et_cos,et_msg_handler,evidence,ethos,camunda,role_assignment,wa_workflow_api,cft_task_db,wa_case_event_messages_db,ccd_data,emstitch'
    environment 'CCD_S2S_AUTHORISED_SERVICES_CASE_USER_ROLES', 'aac_manage_case_assignment,et_cos,et_sya_api,et_msg_handler'
    // ccd-data-store-api
    environment 'DATA_STORE_S2S_AUTHORISED_SERVICES', 'ethos_repl_service,ccd_gw,ccd_gateway,fpl_case_service,ccd_data,ccd_ps,aac_manage_case_assignment,xui_webapp,ccd_case_document_am_api,am_role_assignment_service,ethos_repl_docmosis_service,ecm_consumer,et_cos,et_msg_handler,et_sya_api,wa_workflow_api,wa_task_management_api,wa_task_monitor,wa_case_event_handler,em_ccd_orchestrator,em_stitching_api'
    environment 'CCD_DOCUMENT_URL_PATTERN', '.*'

    // ccd-definition-store-api
    environment 'DEFINITION_STORE_S2S_AUTHORISED_SERVICES', 'ccd_data,ccd_gw,ccd_admin,aac_manage_case_assignment,wa_task_management_api,wa_task_monitor,wa_case_event_handler'
    environment 'WELSH_TRANSLATION_ENABLED', 'false'

    // aac-manage-case-assignment
    environment 'MANAGE_CASE_S2S_AUTHORISED_SERVICES', 'xui_webapp,ccd_data,finrem_case_orchestration,prl_cos_api,et_cos,et_sya_api,ethos_repl_service'

    // ccd-case-document-am-api
    environment 'DM_STORE_BASE_URL', 'http://localhost:5005'
    environment 'CASE_DOCUMENT_S2S_AUTHORISED_SERVICES', 'ccd_case_document_am_api,ccd_gw,xui_webapp,ccd_data,et_cos,et_sya_api,ethos_repl_docmosis_service,em_ccd_orchestrator,em_stitching_api,ethos_repl_service'

    // xui-manage-cases
    environment 'XUI_DOCUMENTS_API', 'http://host.docker.internal:5005'
    environment 'XUI_DOCUMENTS_API_V2', 'http://host.docker.internal:4455'
    environment 'XUI_LD_ID', System.getenv('XUI_LD_ID')
    // Allows latest XUI image without WA by not specifying EMPLOYMENT. Add EMPLOYMENT to enable WA.
    environment 'WA_SUPPORTED_JURISDICTIONS', 'IA,CIVIL,PRIVATELAW,PUBLICLAW'

    // xui-manage-org
    environment 'RD_PROFESSIONAL_API_URL', 'http://localhost:8765'
    environment 'RD_PROFESSIONAL_API_SERVICE', 'http://localhost:8765'
    environment 'PRD_HOST', 'http://localhost:8765'
    environment 'XUI_MANAGE_ORGANISATIONS_CASETYPES', 'ET_EnglandWales,ET_Scotland'

    environment 'SPRING_PROFILES_ACTIVE', 'cftlib'
    // et-ccd-callbacks
    //environment 'SERVICEBUS_FAKE', 'true'*
    environment 'CFTLIB_EXTRA_COMPOSE_FILES', ''

}

pmdMain.maxFailures = 15
pmdTest.maxFailures = 0

pmdTest.ruleSetFiles = files("config/pmd/rulesetTest.xml")
